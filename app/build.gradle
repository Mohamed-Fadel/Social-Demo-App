apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compile_sdk
    buildToolsVersion Versions.build_tool
    defaultConfig {
        applicationId "com.example.newsfeedtestapp"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode rootProject.ext.gitCommitCount
        versionName rootProject.ext.gitTag

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp}"

        buildConfigField 'String', 'BASE_URL', "\"http://jsonplaceholder.typicode.com/\""
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        // We need to sign debug builds with a debug key to make firebase auth happy
        debug {
            storeFile file("../debug.keystore")
            keyAlias "alias"
            keyPassword "password"
            storePassword "password"
        }

        release {
            storeFile file("../release.keystore")
            keyAlias "DeliveryReleaseKey"
            keyPassword "DeliveryP@ssW0rd"
            storePassword "DeliveryP@ssW0rd"
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    applicationVariants.all { variant ->
        // Delete unaligned APKs and manifest junk after
        def assembleTask = "assemble${variant.name.capitalize()}"
        tasks[assembleTask].doLast {
            // Enable by adding "-P notify" to end of your ./gradlew command
            if (project.hasProperty("notify")) {
                "terminal-notifier -message '${assembleTask}-->DONE' -sound 'Ping'".execute()
            }
        }
    }
    dataBinding {
        enabled true
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        abortOnError true
        check 'NewApi', 'InlinedApi'
        fatal 'NewApi', 'InlinedApi'
        enable 'UnusedResources'
        checkReleaseBuilds true
        textOutput 'stdout'
    }

    compileOptions {
        sourceCompatibility = Versions.javaSourceCompatibility
        targetCompatibility = Versions.javaTargetCompatibility
    }
    kotlinOptions {
        jvmTarget = Versions.jvmTarget
    }
}

dependencies {
    implementation project(Modules.data)
    implementation project(Modules.remote)
    implementation project(Modules.domain)
    implementation project(Modules.presentation)

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    implementation Libraries.kotlin_stdlib
    implementation Libraries.core_ktx

    // support & design (android + google) & android arch components
    implementation SupportLibraries.appcompat
    implementation Libraries.google_material
    implementation Libraries.constraint_layout
    implementation Libraries.lifecycle_extension
    implementation SupportLibraries.swipeRefresh
    implementation Libraries.toasty
    implementation Libraries.glide

    // dagger
    implementation Libraries.dagger_android
    implementation Libraries.dagger_android_support
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    kapt Libraries.dagger_compiler
    kapt Libraries.dagger_android_processor

    // reactiveX
    implementation Libraries.rx_android
    implementation Libraries.rx_kotlin

    // network
    implementation Libraries.okhttp
    implementation Libraries.okhttp_logging_interceptor
    implementation Libraries.retrofit
    implementation Libraries.retrofitGson
    implementation Libraries.retrofitRxJava

    // testing
    testImplementation TestLibraries.junit
    androidTestImplementation AndroidTestLibraries.junit
    androidTestImplementation AndroidTestLibraries.espresso
}
